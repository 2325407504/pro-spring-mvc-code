apply plugin: 'base'

buildscript {
    repositories {
        add(new org.apache.ivy.plugins.resolver.URLResolver()) {
            name = 'GitHub'
            addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
        }
    }

    dependencies {
        classpath 'bmuschko:gradle-tomcat-plugin:0.8.1'
    }
}

allprojects {

    apply plugin: 'eclipse'
    apply plugin: 'idea'

    description='Pizzas-R-Us Sample Application'

    group = 'com.apress.prospringmvc'

    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenRepo url: 'http://maven.springframework.org/release'
        mavenCentral()
        mavenRepo url: 'http://maven.springframework.org/milestone'
	mavenRepo url: 'https://repository.jboss.org/nexus/content/repositories/releases/'
    }

    eclipse {
        project.natures "org.springframework.ide.eclipse.core.springnature"
    }
}


webProjects = subprojects.findAll { project -> !project.name.endsWith('samples')}
javaProjects = subprojects.findAll { project -> project.name.endsWith('samples')}

configure(webProjects) {
    apply plugin: 'war'
    apply plugin: 'jetty'
    apply plugin: 'tomcat'
    apply plugin: 'eclipse-wtp'
    
    dependencies() {
        providedCompile 'javax:javaee-web-api:6.0'

        def tomcatVersion = '7.0.23'
        tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
               "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
        tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
            exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
        }
    }
}

configure(javaProjects) {
    apply plugin: 'java'

    dependencies() {
        compile 'javax:javaee-web-api:6.0'
    }
    
}

subprojects {    

    sourceCompatibility=1.6
    targetCompatibility=1.6

    springVersion='3.1.0.RELEASE'
    springSecurityVersion='3.1.0.RELEASE'
    springSwfVersion='2.3.0.RELEASE'
    hibernateCoreVersion='3.6.8.Final'
    hibernateEmVersion='3.5.6-Final'

        hibernateVersion='4.0.0.Final'

    configurations { all*.exclude group: 'commons-logging' }

    dependencies() {
        compile "org.springframework:spring-webmvc:$springVersion"
        compile "org.springframework:spring-jdbc:$springVersion"
        compile "org.springframework:spring-orm:$springVersion"
        compile "org.springframework:spring-aop:$springVersion"
        compile "org.springframework:spring-orm:$springVersion"
        compile 'org.slf4j:jcl-over-slf4j:1.6.4'
        compile 'ch.qos.logback:logback-classic:1.0.0'
        compile 'org.apache.commons:commons-lang3:3.0.1'
        compile 'com.h2database:h2:1.3.162'
        compile 'cglib:cglib-nodep:2.2'
        compile 'org.aspectj:aspectjrt:1.6.12'
        compile 'org.aspectj:aspectjweaver:1.6.12'
        compile "org.hibernate:hibernate-entitymanager:$hibernateVersion"
        compile 'opensymphony:sitemesh:2.4.2'

        runtime "jstl:jstl:1.2"
        
        testCompile 'junit:junit:4.9'
        testCompile "org.springframework:spring-test:$springVersion"

    }
}

task wrapper(type: Wrapper) { gradleVersion = '1.0-milestone-6' }
